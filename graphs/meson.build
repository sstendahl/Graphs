# SPDX-License-Identifier: GPL-3.0-or-later
configure_file(
          input: 'graphs.in',
         output: 'graphs',
  configuration: conf,
        install: true,
   install_mode: 'rwxr-xr-x',
    install_dir: bindir,
)

python.install_sources(
  files(
    'file_import/parsers/__init__.py',
    'file_import/parsers/columns/__init__.py',
    'file_import/parsers/sql/__init__.py',
    'file_import/parsers/project/__init__.py',
    'file_import/parsers/xrdml/__init__.py',
    'file_import/parsers/xry/__init__.py',
    'file_import/__init__.py',
    'sidebar/edit_item.py',
    'style_editor/__init__.py',
    'style_editor/editor_box.py',
    'application.py',
    'artist.py',
    'canvas.py',
    'curve_fitting.py',
    'data.py',
    'export_items.py',
    'figure.py',
    'file_io.py',
    'item.py',
    'migrate.py',
    'misc.py',
    'operations.py',
    'project.py',
    'python_helper.py',
    'scales.py',
    'style_io.py',
    'styles.py',
    'utilities.py',
    'window.py',
  ),
  preserve_path: true,
         subdir: 'graphs',
)

configure_file(
         output: 'config.h',
  configuration: vala_conf,
)
config_dep = valac.find_library('config',
  dirs: join_paths(meson.project_source_root(), 'graphs'),
)
graphs_lib = shared_library('graphs',
  files(
    'file_import/parsers/columns/main.vala',
    'file_import/parsers/columns/parser.vala',
    'file_import/parsers/columns/ui.vala',
    'file_import/parsers/sql/reader.vala',
    'file_import/parsers/sql/ui.vala',
    'file_import/dialog.vala',
    'file_import/file_import.vala',
    'math_parser/evaluator.vala',
    'math_parser/lexer.vala',
    'math_parser/main.vala',
    'sidebar/edit_item.vala',
    'sidebar/figure_settings.vala',
    'sidebar/main.vala',
    'sidebar/operations.vala',
    'style_editor/add_style.vala',
    'style_editor/color_manager.vala',
    'style_editor/color_row.vala',
    'style_editor/editor_box.vala',
    'style_editor/item_color_row.vala',
    'style_editor/window.vala',
    'actions.vala',
    'add_equation.vala',
    'application.vala',
    'canvas.vala',
    'curve_fitting.vala',
    'data.vala',
    'export_figure.vala',
    'export_items.vala',
    'figure_settings.vala',
    'generate_data.vala',
    'item.vala',
    'item_box.vala',
    'misc.vala',
    'project.vala',
    'python_helper.vala',
    'smoothen_settings.vala',
    'styles.vala',
    'transform.vala',
    'utilities.vala',
    'window.vala',
  ), gresource,
             vala_gir: 'Graphs-1.gir',
         dependencies: dependencies + config_dep,
  include_directories: include_directories('.'),
            link_args: ['-lm'],
            vala_args: ['--pkg=posix'],
              install: true,
          install_dir: [true],
)

custom_target('graphs typelib',
       output: 'Graphs-1.typelib',
      depends: graphs_lib,
      install: true,
  install_dir: join_paths(libdir, 'girepository-1.0'),
      command: [
        find_program('g-ir-compiler'),
        '--shared-library',
        'libgraphs.so',
        '--output',
        '@OUTPUT@',
        join_paths(meson.current_build_dir(), 'Graphs-1.gir'),
      ],
 )
devenv.set('GI_TYPELIB_PATH', meson.current_build_dir())
